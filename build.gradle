def checkProperty(Project project, String propName) {
    if (!project.hasProperty(propName)) return false
    String prop = project.property(propName)
    return prop != null && prop.length() > 0
}

def getPropertyOrElse(Project project, String propName, String alternative) {
    if (!checkProperty(project, propName)) return alternative
    return project.property(propName)
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id("com.gradleup.nmcp.aggregation").version("1.0.1")
}

repositories {
    mavenCentral()
}

group = 'org.laxture'
version = "${version}"

dependencies {
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api "org.jooq:jooq:${jooq_version}"
    api 'org.apache.commons:commons-lang3:3.19.0'
    api 'commons-beanutils:commons-beanutils:1.11.0'
    implementation "com.fasterxml.jackson.core:jackson-core:${jackson_version}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jackson_version}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jackson_version}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}"
    implementation "org.springframework.data:spring-data-commons:${spring_boot_version}"

    // Spring Boot AutoConfiguration support (optional)
    compileOnly "org.springframework.boot:spring-boot-autoconfigure:${spring_boot_version}"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${spring_boot_version}"

    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testImplementation 'org.hamcrest:hamcrest:3.0'
    testRuntimeOnly 'com.h2database:h2:2.3.232'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

jar {
    manifest.attributes provider: 'gradle'
    enabled = true
    doFirst {
        archiveFileName = "$project.name-${archiveVersion.get()}.${archiveExtension.get()}"
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    workingDir = project.rootDir
    testLogging {
        events "failed"
        exceptionFormat = "short"
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

Properties localProp = new Properties()
try {
    localProp.load(project.rootProject.file('local.properties').newDataInputStream())
} catch(Exception ignored) {}
for (String propKey in localProp.keys()) {
    ext.set(propKey, localProp.get(propKey))
}
ext."signing.secretKeyRingFile" = rootProject.file('publish.gpg')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.getGroup()
            version "${version}"
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'sbp introduce plugin oriented programming to Spring Boot'
                url = 'https://github.com/hank-cp/sbp'
                organization {
                    name = 'org.laxture'
                    url = 'https://laxture.org'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/hank-cp/sbp/issues'
                }
                license {
                    name = 'Apache License 3.0'
                    url = 'https://github.com/hank-cp/sbp/blob/master/LICENSE'
                    distribution = 'repo'
                }
                scm {
                    url = 'https://github.com/hank-cp/sbp'
                    connection = 'scm:git:git://github.com/hank-cp/sbp.git'
                    developerConnection = 'scm:git:ssh://git@github.com:hank-cp/sbp.git'
                }
                developers {
                    developer {
                        name = 'Hank CP'
                        email = 'true.cp@gmail.com'
                    }
                }
            }
            repositories {
                maven {
                    def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                    def stagingRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : stagingRepoUrl
                    credentials {
                        username getPropertyOrElse(project, 'sonatypeUsername', '')
                        password getPropertyOrElse(project, 'sonatypePassword', '')
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nmcpAggregation {
    centralPortal {
        username = getPropertyOrElse(project, 'sonatypeUsername', '')
        password = getPropertyOrElse(project, 'sonatypePassword', '')
        // publish manually from the portal
        publishingType = "USER_MANAGED"
        // or if you want to publish automatically
//        publishingType = "AUTOMATIC"
    }

    // Publish all projects that apply the 'maven-publish' plugin
    publishAllProjectsProbablyBreakingProjectIsolation()
}